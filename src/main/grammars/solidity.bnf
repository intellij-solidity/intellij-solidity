{
  parserClass="me.serce.solidity.SolidityParser"

  psiClassPrefix="Sol"
  psiImplClassSuffix="Impl"
  psiPackage="me.serce.solidity.lang.psi"
  psiImplPackage="me.serce.solidity.lang.psi.impl"
  implements="me.serce.solidity.lang.psi.SolElement"

  elementTypeHolderClass="me.serce.solidity.lang.core.SolidityTokenTypes"
  elementTypePrefix=""
  elementTypeClass="me.serce.solidity.lang.core.SolElementType"
  tokenTypeClass="me.serce.solidity.lang.core.SolElementType"
  tokenTypeClass="me.serce.solidity.lang.core.SolidityTokenTypes"

  tokens = [
    comment='regexp:(//.*)|(/\\*(?:.|[\\n\\r])*?\\*/)'
    hexLiteral='regexp:hex"([0-9a-fA-F]+)"'
    stringLiteral='regexp:("([^"\r\n\\]|\\.)*")'
    decimalNumber='regexp:([0-9]+)'
    fixedNumber='regexp:(\d+\.\d*|\d*\.\d+)'
    scientificNumber='regexp:((\d+|\d+\.\d*|\d*\.\d+)[Ee][+-]?[0-9]+)'
    hexNumber='regexp:(0x[0-9a-fA-F]+)'
    numberUnit='regexp:(wei|szabo|finney|ether|seconds|minutes|hours|days|weeks|years)'
    intNumType='regexp:int(8|16|24|32|40|48|56|64|72|80|88|96|104|112|120|128|136|144|152|160|168|176|184|192|200|208|216|224|232|240|248|256)?'
    uIntNumType='regexp:uint(8|16|24|32|40|48|56|64|72|80|88|96|104|112|120|128|136|144|152|160|168|176|184|192|200|208|216|224|232|240|248|256)?'
    byteNumType='regexp:byte(1|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31|32)?'
    bytesNumType='regexp:bytes(1|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31|32)?'
    fixedNumType='regexp:fixed(0x8|0x16|0x24|0x32|0x40|0x48|0x56|0x64|0x72|0x80|0x88|0x96|0x104|0x112|0x120|0x128|0x136|0x144|0x152|0x160|0x168|0x176|0x184|0x192|0x200|0x208|0x216|0x224|0x232|0x240|0x248|0x256|8x8|8x16|8x24|8x32|8x40|8x48|8x56|8x64|8x72|8x80|8x88|8x96|8x104|8x112|8x120|8x128|8x136|8x144|8x152|8x160|8x168|8x176|8x184|8x192|8x200|8x208|8x216|8x224|8x232|8x240|8x248|16x8|16x16|16x24|16x32|16x40|16x48|16x56|16x64|16x72|16x80|16x88|16x96|16x104|16x112|16x120|16x128|16x136|16x144|16x152|16x160|16x168|16x176|16x184|16x192|16x200|16x208|16x216|16x224|16x232|16x240|24x8|24x16|24x24|24x32|24x40|24x48|24x56|24x64|24x72|24x80|24x88|24x96|24x104|24x112|24x120|24x128|24x136|24x144|24x152|24x160|24x168|24x176|24x184|24x192|24x200|24x208|24x216|24x224|24x232|32x8|32x16|32x24|32x32|32x40|32x48|32x56|32x64|32x72|32x80|32x88|32x96|32x104|32x112|32x120|32x128|32x136|32x144|32x152|32x160|32x168|32x176|32x184|32x192|32x200|32x208|32x216|32x224|40x8|40x16|40x24|40x32|40x40|40x48|40x56|40x64|40x72|40x80|40x88|40x96|40x104|40x112|40x120|40x128|40x136|40x144|40x152|40x160|40x168|40x176|40x184|40x192|40x200|40x208|40x216|48x8|48x16|48x24|48x32|48x40|48x48|48x56|48x64|48x72|48x80|48x88|48x96|48x104|48x112|48x120|48x128|48x136|48x144|48x152|48x160|48x168|48x176|48x184|48x192|48x200|48x208|56x8|56x16|56x24|56x32|56x40|56x48|56x56|56x64|56x72|56x80|56x88|56x96|56x104|56x112|56x120|56x128|56x136|56x144|56x152|56x160|56x168|56x176|56x184|56x192|56x200|64x8|64x16|64x24|64x32|64x40|64x48|64x56|64x64|64x72|64x80|64x88|64x96|64x104|64x112|64x120|64x128|64x136|64x144|64x152|64x160|64x168|64x176|64x184|64x192|72x8|72x16|72x24|72x32|72x40|72x48|72x56|72x64|72x72|72x80|72x88|72x96|72x104|72x112|72x120|72x128|72x136|72x144|72x152|72x160|72x168|72x176|72x184|80x8|80x16|80x24|80x32|80x40|80x48|80x56|80x64|80x72|80x80|80x88|80x96|80x104|80x112|80x120|80x128|80x136|80x144|80x152|80x160|80x168|80x176|88x8|88x16|88x24|88x32|88x40|88x48|88x56|88x64|88x72|88x80|88x88|88x96|88x104|88x112|88x120|88x128|88x136|88x144|88x152|88x160|88x168|96x8|96x16|96x24|96x32|96x40|96x48|96x56|96x64|96x72|96x80|96x88|96x96|96x104|96x112|96x120|96x128|96x136|96x144|96x152|96x160|104x8|104x16|104x24|104x32|104x40|104x48|104x56|104x64|104x72|104x80|104x88|104x96|104x104|104x112|104x120|104x128|104x136|104x144|104x152|112x8|112x16|112x24|112x32|112x40|112x48|112x56|112x64|112x72|112x80|112x88|112x96|112x104|112x112|112x120|112x128|112x136|112x144|120x8|120x16|120x24|120x32|120x40|120x48|120x56|120x64|120x72|120x80|120x88|120x96|120x104|120x112|120x120|120x128|120x136|128x8|128x16|128x24|128x32|128x40|128x48|128x56|128x64|128x72|128x80|128x88|128x96|128x104|128x112|128x120|128x128|136x8|136x16|136x24|136x32|136x40|136x48|136x56|136x64|136x72|136x80|136x88|136x96|136x104|136x112|136x120|144x8|144x16|144x24|144x32|144x40|144x48|144x56|144x64|144x72|144x80|144x88|144x96|144x104|144x112|152x8|152x16|152x24|152x32|152x40|152x48|152x56|152x64|152x72|152x80|152x88|152x96|152x104|160x8|160x16|160x24|160x32|160x40|160x48|160x56|160x64|160x72|160x80|160x88|160x96|168x8|168x16|168x24|168x32|168x40|168x48|168x56|168x64|168x72|168x80|168x88|176x8|176x16|176x24|176x32|176x40|176x48|176x56|176x64|176x72|176x80|184x8|184x16|184x24|184x32|184x40|184x48|184x56|184x64|184x72|192x8|192x16|192x24|192x32|192x40|192x48|192x56|192x64|200x8|200x16|200x24|200x32|200x40|200x48|200x56|208x8|208x16|208x24|208x32|208x40|208x48|216x8|216x16|216x24|216x32|216x40|224x8|224x16|224x24|224x32|232x8|232x16|232x24|240x8|240x16|248x8)?'
    uFixedNumType='regexp:ufixed(0x8|0x16|0x24|0x32|0x40|0x48|0x56|0x64|0x72|0x80|0x88|0x96|0x104|0x112|0x120|0x128|0x136|0x144|0x152|0x160|0x168|0x176|0x184|0x192|0x200|0x208|0x216|0x224|0x232|0x240|0x248|0x256|8x8|8x16|8x24|8x32|8x40|8x48|8x56|8x64|8x72|8x80|8x88|8x96|8x104|8x112|8x120|8x128|8x136|8x144|8x152|8x160|8x168|8x176|8x184|8x192|8x200|8x208|8x216|8x224|8x232|8x240|8x248|16x8|16x16|16x24|16x32|16x40|16x48|16x56|16x64|16x72|16x80|16x88|16x96|16x104|16x112|16x120|16x128|16x136|16x144|16x152|16x160|16x168|16x176|16x184|16x192|16x200|16x208|16x216|16x224|16x232|16x240|24x8|24x16|24x24|24x32|24x40|24x48|24x56|24x64|24x72|24x80|24x88|24x96|24x104|24x112|24x120|24x128|24x136|24x144|24x152|24x160|24x168|24x176|24x184|24x192|24x200|24x208|24x216|24x224|24x232|32x8|32x16|32x24|32x32|32x40|32x48|32x56|32x64|32x72|32x80|32x88|32x96|32x104|32x112|32x120|32x128|32x136|32x144|32x152|32x160|32x168|32x176|32x184|32x192|32x200|32x208|32x216|32x224|40x8|40x16|40x24|40x32|40x40|40x48|40x56|40x64|40x72|40x80|40x88|40x96|40x104|40x112|40x120|40x128|40x136|40x144|40x152|40x160|40x168|40x176|40x184|40x192|40x200|40x208|40x216|48x8|48x16|48x24|48x32|48x40|48x48|48x56|48x64|48x72|48x80|48x88|48x96|48x104|48x112|48x120|48x128|48x136|48x144|48x152|48x160|48x168|48x176|48x184|48x192|48x200|48x208|56x8|56x16|56x24|56x32|56x40|56x48|56x56|56x64|56x72|56x80|56x88|56x96|56x104|56x112|56x120|56x128|56x136|56x144|56x152|56x160|56x168|56x176|56x184|56x192|56x200|64x8|64x16|64x24|64x32|64x40|64x48|64x56|64x64|64x72|64x80|64x88|64x96|64x104|64x112|64x120|64x128|64x136|64x144|64x152|64x160|64x168|64x176|64x184|64x192|72x8|72x16|72x24|72x32|72x40|72x48|72x56|72x64|72x72|72x80|72x88|72x96|72x104|72x112|72x120|72x128|72x136|72x144|72x152|72x160|72x168|72x176|72x184|80x8|80x16|80x24|80x32|80x40|80x48|80x56|80x64|80x72|80x80|80x88|80x96|80x104|80x112|80x120|80x128|80x136|80x144|80x152|80x160|80x168|80x176|88x8|88x16|88x24|88x32|88x40|88x48|88x56|88x64|88x72|88x80|88x88|88x96|88x104|88x112|88x120|88x128|88x136|88x144|88x152|88x160|88x168|96x8|96x16|96x24|96x32|96x40|96x48|96x56|96x64|96x72|96x80|96x88|96x96|96x104|96x112|96x120|96x128|96x136|96x144|96x152|96x160|104x8|104x16|104x24|104x32|104x40|104x48|104x56|104x64|104x72|104x80|104x88|104x96|104x104|104x112|104x120|104x128|104x136|104x144|104x152|112x8|112x16|112x24|112x32|112x40|112x48|112x56|112x64|112x72|112x80|112x88|112x96|112x104|112x112|112x120|112x128|112x136|112x144|120x8|120x16|120x24|120x32|120x40|120x48|120x56|120x64|120x72|120x80|120x88|120x96|120x104|120x112|120x120|120x128|120x136|128x8|128x16|128x24|128x32|128x40|128x48|128x56|128x64|128x72|128x80|128x88|128x96|128x104|128x112|128x120|128x128|136x8|136x16|136x24|136x32|136x40|136x48|136x56|136x64|136x72|136x80|136x88|136x96|136x104|136x112|136x120|144x8|144x16|144x24|144x32|144x40|144x48|144x56|144x64|144x72|144x80|144x88|144x96|144x104|144x112|152x8|152x16|152x24|152x32|152x40|152x48|152x56|152x64|152x72|152x80|152x88|152x96|152x104|160x8|160x16|160x24|160x32|160x40|160x48|160x56|160x64|160x72|160x80|160x88|160x96|168x8|168x16|168x24|168x32|168x40|168x48|168x56|168x64|168x72|168x80|168x88|176x8|176x16|176x24|176x32|176x40|176x48|176x56|176x64|176x72|176x80|184x8|184x16|184x24|184x32|184x40|184x48|184x56|184x64|184x72|192x8|192x16|192x24|192x32|192x40|192x48|192x56|192x64|200x8|200x16|200x24|200x32|200x40|200x48|200x56|208x8|208x16|208x24|208x32|208x40|208x48|216x8|216x16|216x24|216x32|216x40|224x8|224x16|224x24|224x32|232x8|232x16|232x24|240x8|240x16|248x8)?'
    booleanLiteral='regexp:true|false'
    space='regexp:\s+'

    Identifier='regexp:[a-zA-Z_$][a-zA-Z_$0-9]*'


    DOT='.'
    PLACEHOLDER='_'
    COLON=':'
    SEMICOLON=';'
    COMMA=','
    PLUS='+'
    MINUS='-'
    MULT='*'
    DIV='/'
    EXPONENT='**'
    NOT='!'
    ASSIGN='='
    TO='=>'
    EQ='=='
    NEQ='!='
    INC='++'
    DEC='--'
    PLUS_ASSIGN='+='
    MINUS_ASSIGN='-='
    MULT_ASSIGN='*='
    DIV_ASSIGN='/='
    OR_ASSIGN='|='
    XOR_ASSIGN='^='
    AND_ASSIGN='&='
    LSHIFT_ASSIGN='<<='
    RSHIFT_ASSIGN='>>='
    PERCENT_ASSIGN='%='
    LESS='<'
    LESSEQ='<='
    MORE='>'
    MOREEQ='>='
    CARET='^'
    AND='&'
    ANDAND='&&'
    OR='|'
    OROR='||'
    LBRACKET='['
    RBRACKET=']'
    LBRACE='{'
    RBRACE='}'
    LPAREN='('
    RPAREN=')'
    QUESTION='?'
    PERCENT='%'
    TILDE='~'
    LSHIFT='<<'
    RSHIFT='>>'
    LEFT_ASSEMBLY=':='
    RIGHT_ASSEMBLY='=:'
    DOC_COMMENT_BEGIN='/*'
    DOC_COMMENT_END='*/'

    pragmaAll='regexp:\^[a-zA-Z_0-9\.]*'
  ]

  extends(".*Expression")=Expression
  extends(".*TypeName")=TypeName
}


SourceUnit ::= (PragmaDirective | ImportDirective | ContractDefinition | Expression)*

private UntilSemicolonRecover ::= !(';')
private UntilBraceRecover ::= !('}')

// Pragma actually parses anything up to the trailing ';' to be fully forward-compatible.
private PragmaPart ::= pragma Identifier pragmaAll {
    pin = 2
    recoverWhile = UntilSemicolonRecover
}
PragmaDirective ::= PragmaPart ';'

private ClosedBracketRecover ::= !('}')
ImportAlias ::= Identifier {
    implements = "me.serce.solidity.lang.psi.SolNamedElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolImportAliasMixin"
}
private ImportRuleSet ::= UserDefinedTypeName (as ImportAlias)? ( ',' UserDefinedTypeName (as ImportAlias)? )* {
    recoverWhile = ClosedBracketRecover
}
ImportPath ::= StringLiteral {
    implements = "me.serce.solidity.lang.psi.SolNamedElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolImportPathElement"
}
private ImportDirectivePart ::= import ( ImportPath (as ImportAlias)?
        | ('*' | UserDefinedTypeName) (as ImportAlias)? 'from' ImportPath
        | '{' ImportRuleSet '}' 'from' ImportPath ) {
 pin = 1
 recoverWhile = UntilSemicolonRecover
}
ImportDirective ::= ImportDirectivePart ';'


ContractDefinition ::= ( contract | library | interface ) Identifier
                     ( is InheritanceSpecifier (',' InheritanceSpecifier )* )?
                     '{' ContractPart* '}' {
    pin = 1
    implements = "me.serce.solidity.lang.psi.SolContractOrLibElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolContractOrLibMixin"
    stubClass = "me.serce.solidity.lang.stubs.SolContractOrLibDefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

private ContractPart ::= StateVariableDeclaration | UsingForDeclaration
             | StructDefinition | ModifierDefinition | FunctionDefinition | EventDefinition | EnumDefinition

InheritanceSpecifier ::= UserDefinedTypeName ( '(' Expression ( ',' Expression )* ')' )?

private VisibilityModifiers ::= ( public | internal | private )

StateVariableDeclaration ::= TypeName (VisibilityModifiers constant | constant? VisibilityModifiers? ) Identifier ('=' Expression)? ';'  {
    implements = "me.serce.solidity.lang.psi.SolNamedElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolStateVarDeclMixin"
    stubClass = "me.serce.solidity.lang.stubs.SolStateVarDeclStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

UsingForDeclaration ::= using UserDefinedTypeName for ('*' | TypeName) ';' {pin=1}

StructDefinition ::= struct Identifier '{'
                     ( VariableDeclaration ';' (VariableDeclaration ';')* )? '}' {
    pin=1
    implements = "me.serce.solidity.lang.psi.SolNamedElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolStructDefMixin"
    stubClass = "me.serce.solidity.lang.stubs.SolStructDefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

ModifierDefinition ::= modifier Identifier ParameterList? Block {
    pin = 1
    implements = "me.serce.solidity.lang.psi.SolModifierElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolModifierDefMixin"
    stubClass = "me.serce.solidity.lang.stubs.SolModifierDefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

FunctionModifier ::= StateMutability | external | public | internal | private | Identifier

FunctionDefinition ::= function Identifier? ParameterList
                     ( FunctionCallExpression | FunctionModifier)*
                     ( returns ParameterList )? ( ';' | Block ) {
    pin = 1
    implements = "me.serce.solidity.lang.psi.SolFunctionDefElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolFunctionDefMixin"
    stubClass = "me.serce.solidity.lang.stubs.SolFunctionDefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

EventDefinition ::= event Identifier IndexedParameterList anonymous? ';' {
    pin = 1
    implements = "me.serce.solidity.lang.psi.SolNamedElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolEventDefMixin"
}

EnumValue ::= Identifier {
    implements = "me.serce.solidity.lang.psi.SolEnumDefElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolEnumValueMixin"
}

EnumDefinition ::= enum Identifier '{' EnumValue? (',' EnumValue)* '}' {
    pin = 1
    implements = "me.serce.solidity.lang.psi.SolEnumDefElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolEnumItemImplMixin"
    stubClass = "me.serce.solidity.lang.stubs.SolEnumDefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

IndexedParameterList ::= '(' ( TypeName indexed?  Identifier? (',' TypeName indexed?  Identifier?)* )? ')'
ParameterDef ::= TypeName (constant StorageLocation? | StorageLocation constant?)? Identifier? {
    implements = "me.serce.solidity.lang.psi.SolNamedElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolParameterDefMixin"
}
ParameterList ::= '(' ( ParameterDef (',' ParameterDef)* )? ')'

// semantic restriction: mappings and structs (recursively) containing mappings
// are not allowed in argument lists
private declarationList ::= '(' ( (Identifier | '_') ','? | ',' )*  ')' ;
// TODO: remove storage declaration from all other places
VariableDeclaration ::= var declarationList | TypeName StorageLocation? Identifier {
    implements = "me.serce.solidity.lang.psi.SolNamedElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolVariableDeclarationMixin"
}

//
//TypeName = ElementaryTypeName
//         | UserDefinedTypeName StorageLocation?
//         | Mapping
//         | ArrayTypeName
//         | FunctionTypeName

TypeName ::= ElementaryTypeName
         | MappingTypeName
         | UserDefinedLocationTypeName
         | BytesArrayTypeName
         | ArrayTypeName
         | FunctionTypeName {
    stubClass = "me.serce.solidity.lang.stubs.SolTypeRefStub"
    extends = "me.serce.solidity.lang.psi.impl.SolStubbedElementImpl<?>"
}

BytesArrayTypeName ::= bytesNumType StorageLocation? {
     stubClass = "me.serce.solidity.lang.stubs.SolTypeRefStub"
     elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

ArrayTypeName ::= TypeName '[' Expression? ']' StorageLocation? {
    stubClass = "me.serce.solidity.lang.stubs.SolTypeRefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

UserDefinedLocationTypeName ::= UserDefinedWithLocationTypeName | UserDefinedWithoutLocationTypeName {
    stubClass = "me.serce.solidity.lang.stubs.SolTypeRefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}
private UserDefinedWithLocationTypeName ::= UserDefinedTypeName StorageLocation
private UserDefinedWithoutLocationTypeName ::= UserDefinedTypeName

UserDefinedTypeName ::= Identifier ( '.' Identifier )* {
    implements = "me.serce.solidity.lang.psi.SolReferenceElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolUserDefinedTypeNameImplMixin"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

MappingTypeName ::= mapping '(' (BytesArrayTypeName | ElementaryTypeName  ) '=>' TypeName ')' {
    pin = 1
    stubClass = "me.serce.solidity.lang.stubs.SolTypeRefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

// According to grammar.txt, it should be only TypeNameList after function, but according to reality ...
FunctionTypeName ::= function ParameterList ( internal | external | StateMutability )*
                   ( returns ParameterList )? {
    pin = 1
    stubClass = "me.serce.solidity.lang.stubs.SolTypeRefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

StorageLocation ::= memory | storage
StateMutability ::= pure | constant | view | payable

private UnfinishedBlock ::= '{' Statement* {
    pin = 1
    recoverWhile = UntilBraceRecover
}
Block ::= UnfinishedBlock '}'

private FinishedStatement ::= DoWhileStatement | PlaceholderStatement | ContinueSt | BreakSt | ReturnSt | ThrowSt | SimpleStatement {
    recoverWhile = UntilSemicolonRecover
}
Statement ::= IfStatement | WhileStatement | ForStatement | Block | InlineAssemblyStatement | TupleStatement | ReturnTupleStatement | FinishedStatement ';'

// intentionally not pinned
TupleStatement ::= VariableDeclaration '=' SeqExpression ';'
    | SeqExpression '=' SeqExpression ';'
    | SeqExpression '=' Expression ';'

// intentionally not pinned
ReturnTupleStatement ::= return SeqExpression ';'

private ExpressionStatement ::= Expression
IfStatement ::= if '(' Expression ')' Statement ( else Statement )? {pin = 1}
WhileStatement ::= while '(' Expression ')' Statement {pin = 1}
PlaceholderStatement ::= '_'
private SimpleStatement ::= VariableDefinition | ExpressionStatement
ForStatement ::= for '(' (SimpleStatement)? ';' (Expression)? ';' (ExpressionStatement)? ')' Statement {pin=1}
InlineAssemblyStatement ::= assembly AssemblyBlock {pin=1}
DoWhileStatement ::= do Statement while '(' Expression ')' {pin=1}
ContinueSt ::= continue
BreakSt ::= break
ReturnSt ::= return Expression? {pin=1}
ThrowSt ::= throw
SeqExpression ::= '(' ','? Expression (',' Expression)* ','? ')'
VariableDefinition ::= VariableDeclaration ( '=' Expression )?

Expression ::=
  TernaryExpression | IncrementExpression | FunctionCallExpression | CallExpression | IndexAccessExpression | ParenExpression
  | UnaryExpression
  | ExponentExpression
  | MultDivExpression
  | PlusMinExpression
  | ShiftExpression
  | AndOpExpression
  | XorOpExpression
  | OrOpExpression
  | CompExpression
  | EqExpression
  | AndExpression
  | OrExpression
  | AssignmentExpression
  | MemberAccessExpression
  | InlineArrayExpression
  | PrimaryExpression


AssignmentExpression ::= Expression ('=' | '|=' | '^=' | '&=' | '<<=' | '>>=' | '+=' | '-=' | '*=' | '/=' | '%=') Expression
InlineArrayExpression ::= '[' Expression (',' Expression)* ']'
TernaryExpression ::= Expression '?' Expression ':' Expression
OrExpression ::= Expression '||' Expression
AndExpression ::= Expression '&&' Expression
EqExpression ::= Expression ('==' | '!=') Expression
CompExpression ::= Expression ('<' | '>' | '<=' | '>=') Expression
OrOpExpression ::= Expression '|' Expression
XorOpExpression ::= Expression '^' Expression
AndOpExpression ::= Expression '&' Expression
ShiftExpression ::= Expression ('<<' | '>>') Expression
PlusMinExpression ::= Expression ('+' | '-') Expression
MultDivExpression ::= Expression ('*' | '/' | '%') Expression
ExponentExpression ::= Expression '**' Expression
UnaryExpression ::= ('!' | '~' | delete | '++' | '--' | '+' | '-') Expression {pin=1}
IncrementExpression ::= Expression ('++' | '--')
ParenExpression ::= '(' Expression ')' { pin = 1 }

PrimaryExpression ::= VarLiteral
                  | BooleanLiteral
                  | NumberLiteral
                  | HexLiteral
                  | StringLiteral
                  | ElementaryTypeNameExpression

VarLiteral ::= Identifier {
    implements = "me.serce.solidity.lang.psi.SolReferenceElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolVarLiteralMixin"
}
FunctionCallExpression ::= (Identifier '(' FunctionCallArguments? ')') |
                           ( ( PrimaryExpression | NewExpression | TypeName  ) ( ( '.' Identifier ) | ( '[' Expression? ']' ) )* '(' FunctionCallArguments? ')' )  {
    implements = "me.serce.solidity.lang.psi.SolReferenceElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolFunctionCallElement"
}
CallExpression ::= Expression ( ( '.' Identifier ) | ( '[' Expression ']' ) )* '(' FunctionCallArguments? ')' ('(' FunctionCallArguments? ')')*
FunctionCallArguments ::= MapExpression | Expression? ( ',' Expression )*
private MapExpressionClause ::= Identifier ':' Expression {pin=2}
MapExpression ::= '{' MapExpressionClause (',' MapExpressionClause )* '}' {pin=1}

NewExpression ::= new ( Identifier | PrimitiveLiteral )
MemberAccessExpression ::= Expression '.' Identifier {
    pin = 2
    implements = "me.serce.solidity.lang.psi.SolReferenceElement"
    mixin = "me.serce.solidity.lang.psi.impl.SolMemberAccessElement"
}
IndexAccessExpression ::= Expression '[' Expression? ']'

BooleanLiteral ::= booleanLiteral
NumberLiteral ::= ( HexNumber | DecimalNumber | FixedNumber | ScientificNumber) NumberUnit?
NumberUnit ::= wei | szabo | finney | ether
             | seconds | minutes | hours | days | weeks | years
// https://github.com/ethereum/solidity/pull/832
private HexLiteral ::= hexLiteral
private StringLiteral ::= stringLiteral


private HexNumber ::= hexNumber
private FixedNumber ::= fixedNumber
private DecimalNumber ::= decimalNumber
private ScientificNumber ::= scientificNumber

private PrimitiveLiteral ::= intNumType | uIntNumType | byteNumType | bytesNumType | fixedNumType | uFixedNumType | booleanLiteral | address | string | bool

private ElementaryTypeNameExpression ::= ElementaryTypeName

ElementaryTypeName ::= address | bool | string | var | NumberType {
    stubClass = "me.serce.solidity.lang.stubs.SolTypeRefStub"
    elementTypeFactory = "me.serce.solidity.lang.stubs.ImplKt.factory"
}

NumberType ::= intNumType | uIntNumType | byteNumType | fixedNumType | uFixedNumType

// http://solidity.readthedocs.io/en/develop/assembly.html#parsing-grammar

AssemblyBlock ::= '{' AssemblyItem* '}' {pin=1}

AssemblyItem ::=
    AssemblyBlock
    | FunctionalAssemblyExpression
    | AssemblyLocalDefinition
    | FunctionalAssemblyAssignment
    | AssemblyAssignment
    | LabelDefinition
    | AssemblyIf
    | AssemblySwitch
    | AssemblyFunctionDefinition
    | AssemblyFor
    | SubAssembly
    | LinkerSymbol
    | NumberLiteral
    | StringLiteral
    | HexLiteral
    | Identifier

FunctionalAssemblyExpression ::= (AmbiguousAssemblyBytecode | Identifier) '(' AssemblyItem? ( ',' AssemblyItem )* ')'
AssemblyLocalDefinition ::= let IdentifierOrList ':=' FunctionalAssemblyAssignmentOrIdentifier {pin=1}
FunctionalAssemblyAssignment ::= IdentifierOrList ':=' FunctionalAssemblyAssignmentOrIdentifier {pin=2}
private IdentifierOrList ::= IdentifierList | '(' IdentifierList ')'
private IdentifierList ::= Identifier ( ',' Identifier)*
AssemblyAssignment ::= '=:' Identifier {pin=1}
LabelDefinition ::= Identifier ':'
AssemblyIf ::= 'if' FunctionalAssemblyAssignmentOrIdentifier AssemblyBlock {pin=1}
AssemblySwitch ::= 'switch' FunctionalAssemblyAssignmentOrIdentifier AssemblyCase* ( 'default' AssemblyBlock )? {pin=1}
AssemblyCase ::= 'case' FunctionalAssemblyAssignmentOrIdentifier AssemblyBlock {pin=1}
AssemblyFunctionDefinition ::= 'function' Identifier '(' IdentifierOrList? ')'
    ( '->' IdentifierOrList )? AssemblyBlock {pin=1}
AssemblyFor ::= 'for' ( AssemblyBlock | FunctionalAssemblyExpression)
    FunctionalAssemblyExpression ( AssemblyBlock | FunctionalAssemblyExpression) AssemblyBlock {pin=1}
SubAssembly ::= 'assembly' Identifier AssemblyBlock {pin=1}
LinkerSymbol ::= 'linkerSymbol' '(' StringLiteral ')' {pin=1}
private AmbiguousAssemblyBytecode ::= 'return' | 'address' | 'byte' | 'break' | 'continue' | 'function'
private FunctionalAssemblyAssignmentOrIdentifier ::= FunctionalAssemblyExpression | NumberLiteral | Identifier
